(
Copyright © 2022 Lior Stern

Permission is hereby granted, free of charge, to any person obtaining
a copy of this software and associated documentation files (the
“Software”), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject to
the following conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
)

( disassembler )

%SHORT_MODE_MASK { #20 }
%RETURN_MODE_MASK { #40 }
%KEEP_MODE_MASK { #80 }
%OPCODE_MASK { #1f }

%EMIT { .Console/write DEO }
%NEWLINE { #0a EMIT }
%TAB { #09 EMIT }
%SPACE { #20 EMIT }
%OPEN_PAREN { #28 EMIT }
%CLOSE_PAREN { #29 EMIT }

%OPCODE_LIT { #00 }

|0000
@System
	&vector	$2
	&wst	$1
	&rst	$1
	&_	$4
	&red	$2
	&green	$2
	&blue	$2
	&debug	$1
	&state	$1

|0010
@Console
	&vector	$2
	&read	$1
	&_	$5
	&write	$1
	&error	$1

|00a0
@File
	&vector		$2
	&success	$2
	&stat		$2
	&delete		$1
	&append		$1
	&name		$2
	&length		$2
	&read		$2
	&write		$2

( Zero page variables )
|0000
@ZeroPageGlobals
	&rom-offset	$2
	&instruction	$1
	&opcode		$1
	&keep-mode	$1
	&short-mode	$1
	&return-mode	$1

|0100 ( entry point )
	#0000 .ZeroPageGlobals/rom-offset STZ2
	#0000 ;file-name-buffer-offset STA2
	;on-console .Console/vector DEO2
BRK

( Called when a console event occurs, handles only input events )
@on-console ( -- )
	(
		Read intput file name.
		The cli arguments are injected to the console device
		by the emulator, ending with a newline character.
	)
	.Console/read DEI DUP
	( Write file name to file-name-buffer )
	#0a EQU ,&file-name-end JCN
	;file-name-buffer ;file-name-buffer-offset LDA2 ADD2 STA
	;file-name-buffer-offset LDA2 INC2 ;file-name-buffer-offset STA2
BRK
&file-name-end
	(
		The cli argument was loaded.
		Setup the file device for reading.
	)
	POP
	#00 ;file-name-buffer ;file-name-buffer-offset LDA2 ADD2 STA
	;file-name-buffer .File/name DEO2

@file-read-loop ( -- )
	( read an instruction )
	.File/read DEI DUP
	.File/success DEI2 #0000 EQU2 ;eof JCN2
	.ZeroPageGlobals/instruction STZ

	( Set flags, current instruction is on the stack )
	DUPk DUP
	SHORT_MODE_MASK AND .ZeroPageGlobals/short-mode STZ
	RETURN_MODE_MASK AND .ZeroPageGlobals/return-mode STZ
	KEEP_MODE_MASK AND .ZeroPageGlobals/keep-mode STZ
	OPCODE_MASK AND DUP .ZeroPageGlobals/opcode STZ

	;emit-ram-offset JSR2 TAB

	( opcode is on the stack ) OPCODE_LIT NEQ ,&non-lit-opcode JCN
		;handle-lit JSR2
		NEWLINE
		;file-read-loop JMP2
	&non-lit-opcode

	( Print opcode name )
	#00 .ZeroPageGlobals/opcode LDZ
	#0003 MUL2
	;opcode-name-table ADD2
	LDAk EMIT
	INC2 LDAk EMIT
	INC2 LDA EMIT

	( Emit 2,k or r if the relevant flags are on )
	.ZeroPageGlobals/short-mode LDZ #00 EQU ,&after-print-short JCN
		LIT "2 EMIT
	&after-print-short
	.ZeroPageGlobals/keep-mode LDZ #00 EQU ,&after-print-keep JCN
		LIT "k EMIT
	&after-print-keep
	.ZeroPageGlobals/return-mode LDZ #00 EQU ,&after-print-return JCN
		LIT "r EMIT
	&after-print-return

	TAB ;emit-instruction-as-comment JSR2

	NEWLINE
	.ZeroPageGlobals/rom-offset LDZ2 INC2 .ZeroPageGlobals/rom-offset STZ2
	;file-read-loop JMP2

( Handle LIT BRK or fallback to just raw data )
@handle-lit ( -- )
	.ZeroPageGlobals/return-mode LDZ

	( Instruction is 0x20, 0x40 or 0x60 )
	.ZeroPageGlobals/instruction LDZ
	DUP #20 EQU 	( instruction )
	OVR #40 EQU ORA	( instruction instruction==0x20 )
	SWP #60 EQU ORA ( instruction instruction==0x20|instruction==0x40 )
			( instruction==0x20|instruction==0x40|instruction==0x60 )

	ORA
	;emit-raw-byte JCN2

	.ZeroPageGlobals/keep-mode LDZ #00 EQU ;emit-brk JCN2

	.ZeroPageGlobals/short-mode LDZ ,&emit-short-literal JCN

( Print the instruction and the following byte as a byte literal. Example: `#ff` )
&emit-byte-literal
	.ZeroPageGlobals/rom-offset LDZ2 .File/length DEI2 EQU2 ;emit-raw-byte JCN2
	LIT "# EMIT
	.File/read DEI DUP ( TODO: Check for errors )
	;emit-byte JSR2

	( Print bytes as comment )
	TAB
	OPEN_PAREN
	SPACE
	.ZeroPageGlobals/instruction LDZ ;emit-byte JSR2
	;emit-byte JSR2
	SPACE
	CLOSE_PAREN

	.ZeroPageGlobals/rom-offset LDZ2 INC2 INC2 .ZeroPageGlobals/rom-offset STZ2
JMP2r

( Print the instruction and the following short as a short literal. Example: `#ffff` )
&emit-short-literal
	.ZeroPageGlobals/rom-offset LDZ2 #0003 ADD2 .File/length DEI2 EQU2 ;emit-raw-byte JCN2
	LIT "# EMIT
	.File/read DEI ( TODO: Check for errors )
	.File/read DEI ( TODO: Check for errors )
	DUP2
	;emit-short JSR2

	( Print bytes as comment )
	TAB
	OPEN_PAREN
	SPACE
	.ZeroPageGlobals/instruction LDZ ;emit-byte JSR2
	;emit-short JSR2
	SPACE
	CLOSE_PAREN

	.ZeroPageGlobals/rom-offset LDZ2 #0003 ADD2 .ZeroPageGlobals/rom-offset STZ2
JMP2r

( Print the instruction as raw data. Example: `ff` )
@emit-raw-byte ( -- )
	.ZeroPageGlobals/instruction LDZ ;emit-byte JSR2
	.ZeroPageGlobals/rom-offset LDZ2 INC2 .ZeroPageGlobals/rom-offset STZ2
JMP2r

( Print the BRK instruction )
@emit-brk ( -- )
	LIT "B EMIT
	LIT "R EMIT
	LIT "K EMIT
	TAB ;emit-instruction-as-comment JSR2
	.ZeroPageGlobals/rom-offset LDZ2 INC2 .ZeroPageGlobals/rom-offset STZ2
JMP2r

( Halt the program )
@eof
	#01 .System/state DEO
	BRK ( Exit evaluation context so the state change will take effect )

( Print .Zeropageglobals/instruction's value as a comment )
@emit-instruction-as-comment
	OPEN_PAREN
	SPACE
	.ZeroPageGlobals/instruction LDZ ;emit-byte JSR2
	SPACE
	CLOSE_PAREN
JMP2r

( Print address )
@emit-ram-offset
	LIT "| EMIT
	.ZeroPageGlobals/rom-offset LDZ2 #0100 ADD2
	;emit-short JMP2

( Print a number between 0 to 65535 to the screen in hexadecimal )
@emit-short ( short -- )
	SWP
	;emit-byte JSR2
	;emit-byte JMP2

( Print a number between 0 to 255 to the screen in hexadecimal )
@emit-byte ( byte -- )
	DUP
	#f0 AND #04 SFT ;emit-nibble JSR2
	#0f AND ;emit-nibble JMP2

( Print a number between 0 to 15 to the screen in hexadecimal )
@emit-nibble ( byte -- )
	#00 SWP ;hex-table ADD2 LDA EMIT
JMP2r

@hex-table
"0123456789
"abcdef

@opcode-name-table
"LIT
"INC
"POP
"NIP
"SWP
"ROT
"DUP
"OVR
"EQU
"NEQ
"GTH
"LTH
"JMP
"JCN
"JSR
"STH
"LDZ
"STZ
"LDR
"STR
"LDA
"STA
"DEI
"DEO
"ADD
"SUB
"MUL
"DIV
"AND
"ORA
"EOR
"SFT

@file-name-buffer-offset $2
@file-name-buffer
